# # MATH50003 (2024â€“25)
# # Lab 8: V.1 Fourier Expansions and V.2 Discrete Fourier Transform

# This lab explores the practical implementation of Fourier expansions on a computer,
# in particular, via use the periodic Trapezium rule for approximating the Fourier coefficients.
# This has a number of wonderful properties including interpolating data exactly, and
# can be expressed cleanly in terms of the Discrete Fourier Transform (DFT).
# We also explore the closely related cousins of the Fourier expansion: the Fourierâ€“Taylor expansion
# (for functions with only non-negative Fourier coefficients) and the Cosine expansion (for even functions).

# 
# **Learning Outcomes**
#
# Mathematical knowledge:

# 1. Computing Fourier coefficients and approximating Fourier expansions.
# 2. Extensions to Cosine expansions and discrete cosine coefficients.
# 3. The relationship between regularity and convergence of Fourier expansions.
# 4. The Discrete Fourier Transform and the Discrete Cosine Transform.
# 5. The interpolatory behaviour of approximate Fourier expansions.

# Coding knowledge:

# 1. The QuadGK.jl package and `quadgk` function for black box numerical integration.
# 2. The FFTW.jl package and the `fft` function for applying the Discrete Fourier Transform fast.


# We first load  packages we need including two new ones, FFTW.jl (for the fast Fourier transform)
# and QuadGK.jl (for black-box numerical integration)

using LinearAlgebra, Plots, FFTW, QuadGK, Test


# ## V.1 Fourier Expansions

# We consider the Fourier expansion
# $$
# f(x) = âˆ‘_{k=-âˆž}^âˆž fÌ‚_k {\rm e}^{{\rm i}kÎ¸}
# $$
# and the closely related Fourierâ€“Taylor expansion
# $$
# f(x) = âˆ‘_{k=0}^âˆž fÌ‚_k {\rm e}^{{\rm i}kÎ¸}
# $$
# where
# $$
# fÌ‚_k := {1 \over 2Ï€} âˆ«_0^{2Ï€} {\rm e}^{-{\rm i}kÎ¸} f(x)  {\rm d}x.
# $$
# We will ultimately use the Trapezium rule to compute $fÌ‚_k$ but for now we will use QuadGK.jl
# to get a high-accuracy approximation. QuadGK.jl implements a black-box algorithm for computing
# integrals via `quadgk`, eg.

Ïƒ,Îµ = quadgk(exp, 0, 1) # integrates exp(x) for x âˆˆ [0,1]

# This returns a 2-tuple, the first argument is an approximation to the integral

@test Ïƒ â‰ˆ exp(1)-1

# whilst the second argument is an estimate for the error, which in this case is spot-on:

abs(Ïƒ - (exp(1)-1))

# If we give an extra parameter `atol` we can specify the desired accuracy.
# We can thus compute  approximations to the true Fourier coefficients that are accurate
# to $10^{-12}$ via:

fouriercoefficient(f, k) = quadgk(Î¸ -> f(Î¸)*exp(-im*k*Î¸), 0, 2Ï€, atol=1E-12)[1]/(2Ï€)
fouriercoefficient(exp, 0)

# We can use this to approximate a finite truncation of the Fourier series
# $$
# fâ‚™(Î¸) = âˆ‘_{k=-m}^m fÌ‚â‚– {\rm e}^{{\rm i}kÎ¸}
# $$
# Here we implement this in code for the case of $f(Î¸) = \exp Î¸$:

m = 20 # n = 2m+1 Fourier coefficients
fÌ‚ = [fouriercoefficient(exp, k) for k = -m:m]
F = Î¸ -> [exp(im*k*Î¸) for k = -m:m] # Create the Fourier basis
fâ‚™ = Î¸ -> transpose(F(Î¸))*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, exp.(g); label="f")
plot!(g, real(fâ‚™.(g)); label="Re fâ‚™(Î¸)")
plot!(g, imag(fâ‚™.(g)); label="Im fâ‚™(Î¸)")

# Because of symmetry properties the imaginary part is numerically zero.
# But observe also that the approximation overshoots at $0$ and $2Ï€$, something
# known as _Gibb's phenomenon_.

# ------

# **Problem 1(a)** Repeat the above experiment for the non-symmetric truncation
# $$
# fâ‚™(Î¸) = âˆ‘_{k=-m}^{m-1} fÌ‚â‚– {\rm e}^{{\rm i}kÎ¸}
# $$
# with $n = 2m$ Fourier coefficients, where $m = 20$, 
# for $f(Î¸) = \exp(Î¸)$, $f(Î¸) = \exp(\cos(Î¸))$, and $\exp(\exp({\rm i} Î¸))$. Is the imaginary part still
# numerically zero?  Does the second example appear to converge at $0$ and $2Ï€$? 

m = 20 # n = 2m Fourier coefficients
## TODO: plot the truncated Fourier expansion with coefficients ranging from -m:m-1
## SOLUTION
fÌ‚ = [fouriercoefficient(exp, k) for k = -m:m-1]
F = Î¸ -> [exp(im*k*Î¸) for k = -m:m-1] # Create the Fourier basis
fâ‚™ = Î¸ -> transpose(F(Î¸))*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, exp.(g); label="f")
plot!(g, real(fâ‚™.(g)); label="Re fâ‚™(Î¸)")
plot!(g, imag(fâ‚™.(g)); label="Im fâ‚™(Î¸)")
## The imaginary part is not zero
#
f = Î¸ -> exp(cos(Î¸))
fÌ‚ = [fouriercoefficient(f, k) for k = -m:m-1]
F = Î¸ -> [exp(im*k*Î¸) for k = -m:m-1] # Create the Fourier basis
fâ‚™ = Î¸ -> transpose(F(Î¸))*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, f.(g); label="f")
plot!(g, real(fâ‚™.(g)); label="Re fâ‚™(Î¸)")
plot!(g, imag(fâ‚™.(g)); label="Im fâ‚™(Î¸)")
## The imaginary part is now numerically zero and the approximation is indistinguishable.

#
f = Î¸ -> exp(exp(im*Î¸))
fÌ‚ = [fouriercoefficient(f, k) for k = -m:m-1]
F = Î¸ -> [exp(im*k*Î¸) for k = -m:m-1] # Create the Fourier basis
fâ‚™ = Î¸ -> transpose(F(Î¸))*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, real.(f.(g)); label="Re f")
plot!(g, imag.(f.(g)); label="Im f")
plot!(g, real(fâ‚™.(g)); label="Re fâ‚™") 
plot!(g, imag(fâ‚™.(g)); label="Im fâ‚™")
## The real and imaginary parts match to high accuracy
## END

# **Problem 1(b)**  Plot the finite Fourier-Taylor expansion 
# $$
# fâ‚™(Î¸) = âˆ‘_{k=0}^{ n-1} fÌ‚â‚– {\rm e}^{{\rm i}kÎ¸}
# $$
# where $n = 20$,
# for $f(Î¸) = \exp(Î¸)$, $f(Î¸) = \exp(\cos(Î¸))$, and $\exp(\exp({\rm i} Î¸))$.
# For which of these functions does the approximation appear to converge?

n = 20
## TODO: plot the truncated Fourier-Taylor expansion with coefficients ranging from 0:n-1
## SOLUTION
fÌ‚ = [fouriercoefficient(exp, k) for k = 0:n-1]
T = Î¸ -> [exp(im*k*Î¸) for k = 0:n-1] # Create the Fourier basis
fâ‚™ = Î¸ -> transpose(T(Î¸))*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, exp.(g); label="f")
plot!(g, real(fâ‚™.(g)); label="Re fâ‚™(Î¸)")
plot!(g, imag(fâ‚™.(g)); label="Im fâ‚™(Î¸)")
## The approximation fails
#
f = Î¸ -> exp(cos(Î¸))
fÌ‚ = [fouriercoefficient(f, k) for k = 0:n-1]
fâ‚™ = Î¸ -> transpose(T(Î¸))*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, f.(g); label="f")
plot!(g, real(fâ‚™.(g)); label="Re fâ‚™(Î¸)")
plot!(g, imag(fâ‚™.(g)); label="Im fâ‚™(Î¸)")
## Fails to converge
#
f = Î¸ -> exp(exp(im*Î¸))
fÌ‚ = [fouriercoefficient(f, k) for k = 0:n-1]
fâ‚™ = Î¸ -> transpose(T(Î¸))*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, real.(f.(g)); label="Re f")
plot!(g, imag.(f.(g)); label="Im f")
plot!(g, real(fâ‚™.(g)); label="Re fâ‚™") 
plot!(g, imag(fâ‚™.(g)); label="Im fâ‚™")
## The real and imaginary parts match to high accuracy
## END

# **Problem 1(c)** A cosine expansion is a special case of Fourier series of the form
# $$
# f(Î¸) = âˆ‘_{k=0}^âˆž fÌŒ_k \cos k Î¸.
# $$
# Derive an expression for $fÌŒ_k$ and plot the finite cosine expansion
# $$
# fâ‚™(Î¸) = âˆ‘_{k=0}^{ n-1} fÌŒ_k \cos k Î¸
# $$
# for $f(Î¸) = \exp(Î¸)$ and $f(Î¸) = \exp(\cos(Î¸))$. You may assume $f$ is real
# and the returned coefficients should be real.
# For which of these functions does the approximation appear to converge?

n = 20
function cosinecoefficient(f, k)
    ## TODO: use quadgk to approximate fÌŒ_k
    ## SOLUTION
    ## We can either recall the integral formula or use the following to relate to the Fourier coefficient.
    ## We have
    ## fÌ‚â‚– \exp(ikÎ¸) = fÌ‚â‚– \cos(kÎ¸) + i fÌ‚â‚– \sin(kÎ¸) 
    ## fÌ‚â‚‹â‚– \exp(-ikÎ¸) = fÌ‚â‚‹â‚– \cos(kÎ¸) - i fÌ‚â‚‹â‚– \sin(kÎ¸) 
    ## thus ignoring the sin terms we get fÌŒ_k = fÌ‚â‚– + fÌ‚â‚‹â‚–.
    ## apart from the `k = 0` case.
    ## Note in the case where f is even this simplifies as fÌ‚â‚– = fÌ‚â‚‹â‚–.
    ## So your solution might be different if you used this property.
    if k â‰  0
        real(fouriercoefficient(f, k) + fouriercoefficient(f, -k))
    else
        real(fouriercoefficient(f, 0))
    end
    ## END
end
@test cosinecoefficient(Î¸ -> exp(cos(Î¸)), 5) isa Float64
@test cosinecoefficient(Î¸ -> exp(cos(Î¸)), 5) â‰ˆ 0.0005429263119137845

## TODO: plot the truncated cosine expansion with coefficients ranging from 0:n-1
## SOLUTION
fÌ‚ = [cosinecoefficient(exp, k) for k = 0:n-1]
C = Î¸ -> [cos(k*Î¸) for k = 0:n-1] # Create the Cosine basis
fâ‚™ = Î¸ -> C(Î¸)'fÌ‚ # finite Cosine expansion, we can use ' as C(Î¸) is real now.
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, exp.(g); label="f")
plot!(g, fâ‚™.(g); label="fâ‚™(Î¸)") ## The approximation fails
#
f = Î¸ -> exp(cos(Î¸))
fÌ‚ = [cosinecoefficient(f, k) for k = 0:n-1]
fâ‚™ = Î¸ -> C(Î¸)'*fÌ‚ # finite Fourier series
g = range(0, 2Ï€, 1000) # plotting grid
plot(g, f.(g); label="f")
plot!(g, fâ‚™.(g); label="fâ‚™(Î¸)")
## Matches to high accuracy
## END


# ### V.1.1 Convergence of Fourier series

# Different function have different rates of convergence for their Fourier series,
# which is largely dictated by the rate of decay in their coefficients. We can explore
# how different _regularity_, i.e., the smoothness of the function, influences the rate of decay
# of a Fourier expansion.

# ----

# **Problem 2(a)** Plot  the absolute value of coefficients for the functions $Î¸$, 
# $Î¸ (2Ï€ - Î¸)$ and $Î¸^2 (2Ï€-Î¸)^2$ for $k = 1,â€¦,100$. By scaling the $x$- and $y$-axis logarithmically deduce
# experimentally the rate of decay.

## TODO: plot the coefficients for the three functions with different smoothness properties and deduce the rate of decay.
## SOLUTION
plot(1:100, [abs.(fouriercoefficient(Î¸ -> Î¸, k)) for k = 1:100]; yscale=:log10, xscale=:log10)
plot!(1:100, [abs.(fouriercoefficient(Î¸ -> Î¸ * (2Ï€-Î¸), k)) for k = 1:100]; yscale=:log10, xscale=:log10)
plot!(1:100, [abs.(fouriercoefficient(Î¸ -> Î¸^2 * (2Ï€-Î¸)^2, k)) for k = 1:100]; yscale=:log10, xscale=:log10)

plot!(1:100, (1:100) .^ (-1); linestyle=:dash)
plot!(1:100, (1:100) .^ (-2); linestyle=:dash)
plot!(1:100, 10(1:100) .^ (-4); linestyle=:dash)

## It appears to be O(1/k),  O(1/k^2) and O(1/k^4)

## END

# **Problem 2(b)** Repeat the above for $1/(\cos^2 Î¸ + 1)$ and $1/(25 \cos^2 Î¸ + 1)$, $\exp(\cos Î¸)$, now only scaling the $y$-axis logarithmically. Is the
# convergence algebraic, exponential, or super-exponential? 

## TODO: plot the coefficients for the three functions with different smoothness properties and deduce the rate of decay.

## SOLUTION
plot(1:100, [abs.(fouriercoefficient(Î¸ -> 1/(cos(Î¸)^2+1), k)) for k = 1:100]; yscale=:log10)
plot!(1:100, [abs.(fouriercoefficient(Î¸ -> 1/(25cos(Î¸)^2+1), k)) for k = 1:100]; yscale=:log10)
plot!(1:100, [abs.(fouriercoefficient(Î¸ -> exp(cos(Î¸)), k)) for k = 1:100]; yscale=:log10)

## First two examples are exponential:
plot!(1:100, 2.5 .^ (-(1:100)); linestyle=:dash) # first example is roughly O(2.5^(-k))
plot!(1:100, 1.2 .^ (-(1:100)); linestyle=:dash) # second example is roughly O(1.2^(-k))
## Third example is super-exponential
## END

# ----

# ### V.1.2 Trapezium rule and discrete Fourier coefficients

# Above we use `quadgk` to compute the Fourier integrals, however, this has highly limited the number of coefficients
# we can compute as it becomes prohibitively expensive for large `k`. Instead, we can leverage the Trapezium rule which has
# highly structured error properties for computing Fourier integrals. We can use a modified version of the `trapeziumrule` function
# from Lab 1:

function periodictrapeziumrule(f, n)
    ret = 0.0
    for j = 0:n-1
        ret = ret + f(2Ï€*j/n)
    end
    ret/n
end

f = Î¸ -> exp(cos(Î¸))
@test periodictrapeziumrule(f, 10) â‰ˆ quadgk(f, 0, 2Ï€)[1]/(2Ï€)

# This satisfies the discrete orthogonality property:

n = 5
k = 3
@test [periodictrapeziumrule(Î¸ -> exp(im*(k-â„“)*Î¸), n) for â„“=-20:20] â‰ˆ [mod(â„“,n) == mod(k,n) ? 1 : 0 for â„“=-20:20]

# ### V.1.3 Convergence of Approximate Fourier expansions



# We can use the periodic trapezium rule to compute discrete Fourier coefficients $fÌ‚_k^n$ via:

discretefouriercoefficient(f, k, n) = periodictrapeziumrule(Î¸ -> exp(-im*k*Î¸)f(Î¸), n)

# These satisfy the aliasing formula
# $$
# fÌ‚_k^n = â‹¯ + fÌ‚_{k-n} + fÌ‚_k + fÌ‚_{k+n} + â€¦
# $$
# which we can verify for this case since $fÌ‚_k$ decays very rapidly:

n = 4
@test discretefouriercoefficient(f, 1, n) â‰ˆ  fouriercoefficient(f, 1-2n) + fouriercoefficient(f, 1-n) + fouriercoefficient(f, 1) + fouriercoefficient(f, 1+n) + fouriercoefficient(f, 1+2n)

# ------



# **Problem 3** Implement the following function `discretecosinecoefficient(f, k, n)` that implements the discrete Cosine coefficient defined via
# $$
# c_k^n :=  {1 \over n} âˆ‘_{j=1}^n f(Î¸_j) \cos k Î¸_j \begin{cases} 1 & k = 0 \\ 2 & \hbox{otherwise} \end{cases}
# $$
# where $Î¸_j = Ï€(j-1/2)/n$.

function discretecosinecoefficient(f, k, n)
    ## TODO: implement the above approximation to the coefficient in the cosine expansion
    ## SOLUTION
    ret = 0.0
    for j = 1:n
        Î¸â±¼ = Ï€*(j-1/2)/n
        ret = ret + f(Î¸â±¼)*cos(k*Î¸â±¼)
    end
    if k == 0
        ret/n
    else
        2ret/n
    end
    ## END
end

n = 5
@test [discretecosinecoefficient(Î¸ -> 1, â„“, n) for â„“ = 0:n-1] â‰ˆ [â„“ == 0 ? 1 : 0 for â„“=0:n-1]

k = 3
@test [discretecosinecoefficient(Î¸ -> cos(k*Î¸), â„“, n) for â„“ = 0:n-1] â‰ˆ [â„“ == k ? 1 : 0 for â„“=0:n-1]



# ------

# ## V.2 Discrete Fourier Transform

# The discrete Fourier transform (DFT) expresses the map from function values to discrete Fourier coefficients as a matrix:

discretefouriertransform(n) = [exp(-2im*Ï€/n * k*j) for k=0:n-1,j=0:n-1]/sqrt(n)

n = 10
Q = discretefouriertransform(n)
@test Q'Q â‰ˆ I # Q is unitary
Î¸ = [2Ï€*j/n for j=0:n-1]
## Matches the discrete Fourier coefficient:
@test Q/sqrt(n) * exp.(Î¸) â‰ˆ [discretefouriercoefficient(exp, k, n) for k=0:n-1]


# FFTW.jl gives its own implementation of the DFT (up to multiplication by $\sqrt n$) computed via the Fast Fourier Transform (FFT):

@test fft(exp.(Î¸)) â‰ˆ sqrt(n) * Q * exp.(Î¸)

# Matrix multiplication is $O(n^2)$ operations whilst the FFT costs $O(n \log n)$ operations, which is
# a substantial speedup.

# **Problem 4** Use `fft` in the function `specialsum(n)` to construct a vector to compute $[S_n(0),â€¦,S_n(n-1)]$ for
# $$
#    S_n(k) := âˆ‘_{p=0}^âˆž {1 \over (k+p n)!}.
# $$

function specialsum(n)
    ## TODO: implement a numerical algorithm using fft to compute [S_n(0),â€¦,S_n(n-1)], fast enough that the test passes
    ## SOLUTION
    Î¸ = range(0,2Ï€,n+1)[1:end-1] # == [2Ï€*j/n for j=0:n-1]
    fft(exp.(exp.(im .* Î¸)))/n
    ## END
end


n = 5
@test specialsum(n) â‰ˆ [sum(1/factorial(big(k+p*n)) for p = 0:10) for k=0:n-1]
n = 100_000
@test length(specialsum(n)) == n
@test specialsum(n)[1:10] â‰ˆ [1/factorial(k) for k=0:9]
# -----

# ### V.2.1 Trigonometric Interpolation

# An important property of approximate Fourier expansions is that they interpolate the data at the specified grid.
# In the case of the Fourierâ€“Taylor expansion we can observe this by plotting the real (and imaginary) part of the expansion:


g = range(0,2Ï€,1000) # plotting grid
f = Î¸ -> exp(exp(im*Î¸)) # function with a converging Fourierâ€“Taylor expansion
plot(g, real.(f.(g)); label="exp(exp(iÎ¸))")
n = 4
Î¸ = [2Ï€/n*j for j=0:n-1]
ðŸâ‚– = fft(f.(Î¸))/n
fâ‚™ = Î¸ -> transpose([exp(im*k*Î¸) for k=0:n-1])ðŸâ‚–
plot!(g, real.(fâ‚™.(g)); label="n = $n")
scatter!(Î¸, real.(f.(Î¸)); label=nothing) # we interpolate exactly at the grid points

# This example converges very rapidly (and with `n=6` the two curves would be indistinguishable).
# But the interpolation property is true even for functions where we fail to converge:

f = Î¸ -> exp(Î¸) # function without a converging Fourierâ€“Taylor expansion
plot(g, real.(f.(g)); label="exp(Î¸)")
n = 4
Î¸ = [2Ï€/n*j for j=0:n-1]
ðŸâ‚– = fft(f.(Î¸))/n
fâ‚™ = Î¸ -> transpose([exp(im*k*Î¸) for k=0:n-1])ðŸâ‚–
plot!(g, real.(fâ‚™.(g)); label="n = $n")
scatter!(Î¸, real.(f.(Î¸)); label=nothing) # we still interpolate exactly at the grid points

# ------

# **Problem 5(a)** Plot the real part of the approximate Fourier expansion
# $$
# f_{-m:m}(Î¸) : = âˆ‘_{k=-m}^m fÌ‚_k^n {\rm e}^{{\rm i}kÎ¸}
# $$
# and verify that it interpolates $f(Î¸) = \exp(Î¸)$ at the specified grid for $m=5,10,20$. Does the approximation appear to converge?

## TODO: Compare $\exp(Î¸)$ to its approximate Fourier expansion and describe the observed convergence
## SOLUTION

g = range(0,2Ï€,1000)
plot(g, exp.(g); label="exp")
m = 5
ðŸâ‚– = [discretefouriercoefficient(exp, k, 2m+1) for k =-m:m]
fâ‚™ = Î¸ -> transpose([exp(im*k*Î¸) for k=-m:m])ðŸâ‚–
plot!(g, real.(fâ‚™.(g)); label="m = $m")
Î¸ = [2Ï€/(2m+1)*j for j=0:2m]
scatter!(Î¸, exp.(Î¸)) # we interpolate
#
m = 10
plot(g, exp.(g); label="exp")
ðŸâ‚– = [discretefouriercoefficient(exp, k, 2m+1) for k =-m:m]
fâ‚™ = Î¸ -> transpose([exp(im*k*Î¸) for k=-m:m])ðŸâ‚–
plot!(g, real.(fâ‚™.(g)); label="m = $m")
Î¸ = [2Ï€/(2m+1)*j for j=0:2m]
scatter!(Î¸, exp.(Î¸)) # we interpolate
#
m = 20
plot(g, exp.(g); label="exp")
ðŸâ‚– = [discretefouriercoefficient(exp, k, 2m+1) for k =-m:m]
fâ‚™ = Î¸ -> transpose([exp(im*k*Î¸) for k=-m:m])ðŸâ‚–
plot!(g, real.(fâ‚™.(g)); label="m = $m")
Î¸ = [2Ï€/(2m+1)*j for j=0:2m]
scatter!(Î¸, exp.(Î¸)) # we interpolate

## it appears to converge away from 0 and 2Ï€.


## END



# **Problem 5(b)** Consider the approximate cosine expansion
# $$
# f_n(Î¸) = âˆ‘_{k=0}^{n-1} c_k^n \cos k Î¸.
# $$
# Compare $f_n$ and $f$ for  $f(Î¸) = \exp Î¸$  and $\exp(\cos(Î¸))$ on $[0,Ï€]$ for $n = 5, and 20$.
# Does the approximate cosine expansion interpolate both functions? Expain how the convergence properties better than Fourier, even for non-even functions.
# Can you explain this phenomena?

## TODO: by plotting the cosine expansion make some observations on the interpolation and convergence


g = range(0,Ï€,1000)
plot(g, exp.(g); label="exp")
n = 5
ðœâ‚– = [discretecosinecoefficient(exp, k, n) for k =0:n-1]
fâ‚™ = Î¸ -> [cos(k*Î¸) for k=0:n-1]'ðœâ‚–
plot!(g, fâ‚™.(g); label="n = $n")
Î¸ = [Ï€*(j-1/2)/n for j=1:n]
scatter!(Î¸, exp.(Î¸)) # we interpolate
#
plot(g, exp.(g); label="exp")
n = 20
ðœâ‚– = [discretecosinecoefficient(exp, k, n) for k =0:n-1]
fâ‚™ = Î¸ -> [cos(k*Î¸) for k=0:n-1]'ðœâ‚–
plot!(g, fâ‚™.(g); label="n = $n")
Î¸ = [Ï€*(j-1/2)/n for j=1:n]
scatter!(Î¸, exp.(Î¸)) # we interpolate, but this time we converge!!
#
g = range(0,Ï€,1000)
f = Î¸ -> exp(cos(Î¸))
plot(g, f.(g); label="exp(cos(Î¸))")
n = 5
ðœâ‚– = [discretecosinecoefficient(f, k, n) for k =0:n-1]
fâ‚™ = Î¸ -> [cos(k*Î¸) for k=0:n-1]'ðœâ‚–
plot!(g, fâ‚™.(g); label="n = $n")
Î¸ = [Ï€*(j-1/2)/n for j=1:n]
scatter!(Î¸, f.(Î¸)) # we interpolate, but even with just 5 points it matches the function exactly to the eye
#
plot(g, f.(g); label="exp(cos(Î¸))")
n = 20
ðœâ‚– = [discretecosinecoefficient(f, k, n) for k =0:n-1]
fâ‚™ = Î¸ -> [cos(k*Î¸) for k=0:n-1]'ðœâ‚–
plot!(g, fâ‚™.(g); label="n = $n")
Î¸ = [Ï€*(j-1/2)/n for j=1:n]
scatter!(Î¸, f.(Î¸)) # still matches exactly

## The explanation is probably not immediately obvious but results since a cosine expansion approximates a "reflection" of
## the input function that makes it even, that is in the case of exp(Î¸) we are actually approximating


g = range(-2Ï€,2Ï€,1000)
f_even = Î¸ -> if 0 â‰¤ Î¸ â‰¤ Ï€
    exp(Î¸)
elseif Ï€ â‰¤ Î¸ â‰¤ 2Ï€
    exp(2Ï€-Î¸)
elseif -Ï€ â‰¤ Î¸ â‰¤ Ï€
    exp(-Î¸)
elseif -2Ï€ â‰¤ Î¸ â‰¤ -Ï€
    exp(2Ï€+Î¸)
end

plot(g, f_even.(g))
## This extension matches $\exp(Î¸)$ exactly on $[0,Ï€]$ but is periodic and continuous, hence has better convergence properties
## than our original Fourier example.

## END
